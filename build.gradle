plugins {
    id "com.github.spotbugs" version '4.6.2' apply false
    id 'org.springframework.boot' version '2.5.1' apply false
    id 'io.spring.dependency-management' version '1.0.11.RELEASE' apply false
    id 'io.freefair.lombok' version '5.3.0' apply false

}
repositories {
    mavenCentral()
}

ext {
    set('awsSdkV2Version', '2.16.87')
    set('springBootVersion', '2.5.1')
    set('commonsTextVersion', '1.9')
    set('testcontainersVersion', '1.15.3')
    set('jacksonVersion', '2.12.3')
    set('jetbrainsAnnotationsVersion', '17.0.0')
    set('guavaVersion', '27.1-jre')
}

// Gralde will configure all subfolders as projects, and we don't want messaging to be a project
configure(subprojects.findAll { it.name != 'messaging' }) {
    apply plugin: "java"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "com.github.spotbugs"
    apply plugin: "io.freefair.lombok"

    repositories {
        mavenCentral()
    }


    dependencyManagement {
        imports {
            mavenBom("software.amazon.awssdk:bom:${awsSdkV2Version}")
            mavenBom("org.testcontainers:testcontainers-bom:${testcontainersVersion}")
            mavenBom("com.fasterxml.jackson:jackson-bom:${jacksonVersion}")
        }
        dependencies {
            dependency("org.springframework.boot:spring-boot-autoconfigure:${springBootVersion}")
            dependency "org.apache.commons:commons-text:${commonsTextVersion}"
            dependency "org.jetbrains:annotations:${jetbrainsAnnotationsVersion}"
        }
    }

    dependencies {
        compileOnly("org.jetbrains:annotations")
        implementation("org.slf4j:slf4j-api:1.7.31")
        implementation("jakarta.annotation:jakarta.annotation-api:1.3.5")
        implementation("com.google.guava:guava:${guavaVersion}")

    }

}